/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name: Control Concepts, Inc.
System Name:Biamp Tesira Router Control v1.5
System Number:
Programmer: JJM
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
// #ENABLE_DYNAMIC
// #SYMBOL_NAME ""
// #HINT ""
#DEFINE_CONSTANT TRUE 1
#DEFINE_CONSTANT FALSE 0
#DEFINE_CONSTANT MAX_COMMAND_SIZE 150

#DEFINE_CONSTANT RC_CLASS_CODE 1539

// #CATEGORY "" 
// #PRINT_TO_TRACE
// #DIGITAL_EXPAND 
// #ANALOG_SERIAL_EXPAND 
// #OUTPUT_SHIFT 
// #HELP_PDF_FILE ""
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
//#ENABLE_TRACE
#ENCODING_ASCII
// #ENCODING_UTF16
// #ENCODING_INHERIT_FROM_PARENT
// #ENCODING_INHERIT_FROM_PROGRAM

#HELP_BEGIN
To be used with Biamp Tesira Router Control v1.4.umc module only.
Not to be used as a stand-alone module.
#HELP_END

/*******************************************************************************************
  Include Libraries
  (Uncomment and include additional libraries as needed)
*******************************************************************************************/

/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
DIGITAL_INPUT _SKIP_,_SKIP_,_SKIP_; 
DIGITAL_INPUT Poll_Router,_SKIP_;
DIGITAL_INPUT Route_Input, Deroute_Input, Toggle_Input, _SKIP_; 
ANALOG_INPUT New_Input,_SKIP_;
BUFFER_INPUT From_Processor$[1024];

DIGITAL_OUTPUT _SKIP_,_SKIP_,_SKIP_;
DIGITAL_OUTPUT Is_Initialized,_SKIP_;
DIGITAL_OUTPUT _SKIP_,_SKIP_,_SKIP_,_SKIP_; 
ANALOG_OUTPUT Output_Routed, _SKIP_;
STRING_OUTPUT To_Processor$;

/*******************************************************************************************
  SOCKETS
  (Uncomment and define socket definitions as needed)
*******************************************************************************************/

/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/
// INTEGER_PARAMETER
// SIGNED_INTEGER_PARAMETER
// LONG_INTEGER_PARAMETER
// SIGNED_LONG_INTEGER_PARAMETER
STRING_PARAMETER InstanceTag[50], RouterType[50];
INTEGER_PARAMETER Output;

/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/
#BEGIN_PARAMETER_PROPERTIES InstanceTag, RouterType
   propValidUnits = unitString;
   propDefaultUnit = unitString;
   propDefaultValue = "";
#END_PARAMETER_PROPERTIES

#BEGIN_PARAMETER_PROPERTIES Output
   propValidUnits = unitDecimal;
   propDefaultUnit = unitDecimal;
   propBounds = 1d , 256d;
   propDefaultValue = 1d;
#END_PARAMETER_PROPERTIES

/*
#BEGIN_PARAMETER_PROPERTIES parameter_variable, parameter_variable, ...
   // propValidUnits = // unitString or unitDecimal|unitHex|unitPercent|unitCharacter|unitTime|unitTicks;
   // propDefaultUnit = // unitString, unitDecimal, unitHex, unitPercent, unitCharacter, unitTime or unitTicks;
   // propBounds = lower_bound , upper_bound;
   // propDefaultValue = ;  // or, propDefaultValue = "";
   // propList = // { "value" , "label" } , { "value" , "label" } , ... ;
   // propShortDescription = "status_bar_hint_text";
   // #BEGIN_PROP_FULL_DESCRIPTION  line_1...  line_2...  line_n  #END_PROP_FULL_DESCRIPTION
   // #BEGIN_PROP_NOTES line_1...  line_2...  line_n  #END_PROP_NOTES
#END_PARAMETER_PROPERTIES
*/

/*******************************************************************************************
  Structure Definitions
  (Uncomment and define structure definitions as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: struct.myString = "";
*******************************************************************************************/

/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/
INTEGER myID;
INTEGER myClassCode;

INTEGER parsingModuleBusy;
STRING parsedModuleMsg[254];
INTEGER responseModuleMsgID;         
STRING responseRequestMsg[MAX_COMMAND_SIZE];
STRING responseMsg[MAX_COMMAND_SIZE];           
STRING parseValue[MAX_COMMAND_SIZE];        
INTEGER hasSubscriptionRegistered;            
STRING trash[MAX_COMMAND_SIZE];

INTEGER Index1;
INTEGER Index2;       
/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/
////////////////////////////////////////////////////////////
// Module Communication Method Helpers
////////////////////////////////////////////////////////////
String_Function getBoundString(STRING source, STRING startString, STRING endString)
{
	INTEGER startIndex;
	INTEGER endIndex;
	STRING response[MAX_COMMAND_SIZE];
                                     
    response = "";

	IF (LEN(source) > 0)
	{
		startIndex = FIND(startString, source);
		endIndex = FIND(endString, source, startIndex + 1);
	
		IF (startIndex > 0 && startIndex < endIndex)
		{
			startIndex = startIndex + LEN(startString);
				
			response = MID(source, startIndex, endIndex - startIndex);
		}
	}

	RETURN (response);
}
 
////////////////////////////////////////////////////////////
// Protocol Method Helpers	
////////////////////////////////////////////////////////////
String_Function buildProtocolMsg(STRING command, STRING control)
{
	STRING msg[MAX_COMMAND_SIZE];

	msg = "";
                            
	IF (Index1 > 0 && Index2 > 0)
		MakeString(msg,"%s %s %s %d %d", InstanceTag, command, control, Index1, Index2);
	ELSE IF (Index1 > 0 && Index2 = 0)
		MakeString(msg,"%s %s %s %d", InstanceTag, command, control, Index1);
	ELSE IF (Index1 = 0 && Index2 > 0)
		MakeString(msg,"%s %s %s %d", InstanceTag, command, control, Index2);
	ELSE
		MakeString(msg,"%s %s %s", InstanceTag, command, control);		

	RETURN (msg);
}

String_Function buildProtocolMsg_WithStringValue(STRING command, STRING control, STRING value)
{
	STRING msg[MAX_COMMAND_SIZE];

	msg = buildProtocolMsg(command, control);

	MakeString(msg,"%s %s", msg, value);

	RETURN (msg);
}

String_Function buildProtocolMsg_WithIntegerValue(STRING command, STRING control, INTEGER value)
{
	STRING msg[MAX_COMMAND_SIZE];

	msg = buildProtocolMsg(command, control);

	MakeString(msg,"%s %d", msg, value);

	RETURN (msg);
}
////////////////////////////////////////////////////////////
// Initialize Method Helpers	
////////////////////////////////////////////////////////////
Function getAlias()
{
	STRING outgoingMsg[100];

	MakeString(outgoingMsg,"SESSION get alias %s", InstanceTag);
	MakeString(To_Processor$,"POLL_MSG<%d|%s>", myID, outgoingMsg);
}

Function getInitialized()
{
	STRING outgoingMsg[100];
	STRING subscribeObject[100];

	hasSubscriptionRegistered = FALSE;

	IF (RouterType = "input")
	{
		Index2 = Output;

		outgoingMsg = buildProtocolMsg("get", RouterType);
		MakeString(To_Processor$,"POLL_MSG<%d|%s>", myID, outgoingMsg);
		
	}
	ELSE IF (RouterType = "sourceSelection")
	{
		IF (myClassCode = RC_CLASS_CODE)
		{
			Index1 = Output;
			Index2 = 0;
			
			outgoingMsg = buildProtocolMsg("get", RouterType); 
			MakeString(To_Processor$,"POLL_MSG<%d|%s>", myID, outgoingMsg);
		}		
		ELSE
		{
			Index2 = 0;

			MakeString(subscribeObject,"\x22[%d*%s]\x22", myID, RouterType);

			outgoingMsg = buildProtocolMsg_WithStringValue("subscribe", RouterType, subscribeObject); 
			MakeString(To_Processor$,"POLL_MSG<%d|%s>", myID, outgoingMsg);
			
			outgoingMsg = buildProtocolMsg("get", RouterType); 
			MakeString(To_Processor$,"POLL_MSG<%d|%s>", myID, outgoingMsg);
		}
		
	}
}

Function doPoll()
{
	STRING outgoingMsg[100];
	STRING subscribeObject[100];
                               
	IF (hasSubscriptionRegistered = FALSE)
	{
		IF (RouterType = "input")
		{
			outgoingMsg = buildProtocolMsg("get", RouterType);
			MakeString(To_Processor$,"POLL_MSG<%d|%s>", myID, outgoingMsg);
			
		}
		ELSE IF (RouterType = "sourceSelection")
		{		
			outgoingMsg = buildProtocolMsg("get", RouterType); 
			MakeString(To_Processor$,"POLL_MSG<%d|%s>", myID, outgoingMsg);
			
		}
	}
}

////////////////////////////////////////////////////////////
// String Method Helpers
////////////////////////////////////////////////////////////
Integer_Function startsWith(STRING MATCH_STRING, STRING SOURCE_STRING)
{
	RETURN (FIND(MATCH_STRING, SOURCE_STRING) = 1); 
}

Integer_Function contains(STRING MATCH_STRING, STRING SOURCE_STRING)
{
	RETURN (FIND(MATCH_STRING, SOURCE_STRING) > 0); 
}
                 
Integer_Function ATOI_Signed(STRING value)
{
	IF (startsWith("-", value))
		RETURN (0-ATOI(value));
	ELSE
		RETURN (ATOI(value));
}

String_Function trim(STRING value)
{
	WHILE(LEFT(value, 1) = "\x20")
		value = RIGHT(value, LEN(value) - 1);

	WHILE(RIGHT(value, 1) = "\x20")
		value = LEFT(value, LEN(value) - 1);

	RETURN (value);
}

////////////////////////////////////////////////////////////
// Router Output Method Helpers
////////////////////////////////////////////////////////////

/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/
PUSH Route_Input
{
	IF (Is_Initialized = TRUE)
	{
		IF (New_Input >= 0 && New_Input <= 256)
		{
			IF (Output_Routed != New_Input)
			{
				MakeString(To_Processor$,"COMMAND_MSG<%d|%s>", myID, buildProtocolMsg_WithIntegerValue("set", RouterType, New_Input));			
			}
		}
	}
}

PUSH Deroute_Input
{
	IF (Is_Initialized = TRUE)
	{
		IF (New_Input > 0 && New_Input <= 256)
		{
			IF (Output_Routed = New_Input)
			{
				MakeString(To_Processor$,"COMMAND_MSG<%d|%s>", myID, buildProtocolMsg_WithIntegerValue("set", RouterType, 0));			
			}
		}
	}
}

PUSH Toggle_Input
{
	IF (Is_Initialized = TRUE)
	{
		IF (New_Input > 0 && New_Input <= 256)
		{
			IF (Output_Routed = New_Input)
				MakeString(To_Processor$,"COMMAND_MSG<%d|%s>", myID, buildProtocolMsg_WithIntegerValue("set", RouterType, 0));	
			ELSE		
				MakeString(To_Processor$,"COMMAND_MSG<%d|%s>", myID, buildProtocolMsg_WithIntegerValue("set", RouterType, New_Input));	
		}
	}
}

PUSH Poll_Router
{
	IF (Is_Initialized = TRUE)
		doPoll();
}


// Function for processing message received from Processor via CHANGE From_Processor$
FUNCTION ProcessProcessorMsg()
{
    IF (LEN(parsedModuleMsg) > 0)
    {
        IF (contains("REGISTER", parsedModuleMsg))
        {
            myID = ATOI(getBoundString(parsedModuleMsg,"<",">"));
            
            IF (myID > 0)
            {
                MakeString(To_Processor$,"REGISTER<%d>", myID);
            }
        }
        ELSE IF (contains("INIT", parsedModuleMsg))
        {
            Is_Initialized = FALSE;
            getAlias();
        }
        ELSE IF (contains("RESPONSE_MSG", parsedModuleMsg))
        {
            responseRequestMsg = getBoundString(parsedModuleMsg,"<","|");
            responseMsg = getBoundString(parsedModuleMsg,"|",">");

            MakeString(To_Processor$,"RESPONSE_OK<%d|%s>", myID, responseRequestMsg);
                 
            IF (contains("get", responseRequestMsg))
            {
                IF (contains(RouterType, responseRequestMsg))
                {
                    IF (contains("+OK", responseMsg))
                    {			
                        trash = REMOVE("+OK ",responseMsg);

                        Output_Routed = ATOI(responseMsg);	
                        
                    }

                    IF (Is_Initialized = FALSE)
                    {
                        Is_Initialized = TRUE;
                        MakeString(To_Processor$,"INIT_DONE<%d>", myID);
                    }
                }
                ELSE IF (contains("alias", responseRequestMsg))
                {
                    IF (contains("+OK", responseMsg))
                    {
                        trash = REMOVE("+OK [",responseMsg);

                        myClassCode = ATOI(getBoundString(responseMsg," "," "));

                        getInitialized();
                    }
                }						
            }
            ELSE IF (contains("set", responseRequestMsg))
            {
                IF (contains(RouterType, responseRequestMsg))
                {
                    IF (contains("+OK", responseMsg))
                    {			
                        trash = REMOVE(buildProtocolMsg("set", RouterType), responseRequestMsg);
                        responseRequestMsg = trim(responseRequestMsg);
                        Output_Routed = ATOI(responseRequestMsg);
                        
                    }
                }
            }
            ELSE IF (contains("subscribe", responseRequestMsg))
            {
                IF (contains(RouterType, responseRequestMsg))
                {
                    IF (contains("+OK", responseMsg))
                    {			
                        hasSubscriptionRegistered = TRUE;
                    }
                }
            }						
        }
        ELSE IF (contains("SUBSCRIBED_MSG", parsedModuleMsg))
        {
            responseMsg = getBoundString(parsedModuleMsg,"<",">");

            IF (contains(RouterType, responseMsg))
            {							
                trash = REMOVE("]\x22 ",responseMsg);
                
                Output_Routed = ATOI(responseMsg);
            }
        }
    }
}


#if_series3
THREADSAFE CHANGE From_Processor$
{
    WHILE (TRUE)
    {
        try
        {
			parsedModuleMsg = GATHER(">", From_Processor$);

			IF (contains(parsedModuleMsg, From_Processor$))
				clearBuffer(From_Processor$);

            ProcessProcessorMsg();        
        }
        catch
        {
            print("Issue with Processor message handeling\n");
        }
    }
}
#else
CHANGE From_Processor$
{
	IF (parsingModuleBusy = FALSE)
	{
		parsingModuleBusy = TRUE;
                       
		WHILE (TRUE)
		{           
			parsedModuleMsg = GATHER(">", From_Processor$);

			IF (contains(parsedModuleMsg, From_Processor$))
				clearBuffer(From_Processor$);

            ProcessProcessorMsg();
		}

		parsingModuleBusy = FALSE;
	}
}
#endif


/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/

Function Main()
{
	parsingModuleBusy = FALSE;

	Is_Initialized = FALSE;
	hasSubscriptionRegistered = FALSE;

	Index1 = 0;

    //WaitForInitializationComplete();
}


