/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name:
System Name:
System Number:
Programmer: DG
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/

#SYMBOL_NAME "Planar PS-Series (PS4661T_PS5561T) v1.0"
#CATEGORY "24" // TV/Video Projector
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
//#ENABLE_TRACE
  
// UNUSED
// #ENABLE_DYNAMIC
// #HINT ""
// #PRINT_TO_TRACE
// #DIGITAL_EXPAND 
// #ANALOG_SERIAL_EXPAND 
// #OUTPUT_SHIFT 
// #HELP_PDF_FILE ""
// #ENCODING_ASCII
// #ENCODING_UTF16
// #ENCODING_INHERIT_FROM_PARENT
// #ENCODING_INHERIT_FROM_PROGRAM

// MAX STUFF
#DEFINE_CONSTANT MAX_QUEUE_ITEMS 				50
#DEFINE_CONSTANT MAX_COMMAND_SIZE 				255
#DEFINE_CONSTANT MAX_RESPONSE_SIZE 				255
#DEFINE_CONSTANT MAX_BUFFER_SIZE 				10000
#DEFINE_CONSTANT MAX_INPUTS 					4
#DEFINE_CONSTANT MAX_WAKE_MODES					3
#DEFINE_CONSTANT MAX_REMOTE_COMMANDS			8
#DEFINE_CONSTANT MAX_VOL_LVL 					100
#DEFINE_CONSTANT MAX_BACKLIGHT_LVL				100 
#DEFINE_CONSTANT MAX_TOTAL_MONITORS				25

// MIN STUFF
#DEFINE_CONSTANT MIN_VOL_LVL 					0
#DEFINE_CONSTANT MIN_BACKLIGHT_LVL				0
#DEFINE_CONSTANT MIN_TOTAL_MONITORS				1

// VALUE RESET
#DEFINE_CONSTANT BLANK_STRING 					""
#DEFINE_CONSTANT NULL		 					65535

// OTHER STUFF
#DEFINE_CONSTANT TRUE 							1
#DEFINE_CONSTANT FALSE 							0
#DEFINE_CONSTANT QTY_OF_QUEUES 					2
#DEFINE_CONSTANT POLL_TIME 						2000 // 20 seconds
#DEFINE_CONSTANT HEARTBEAT_TIME 				3000 // 30 seconds

// STATIC MESSAGE DATA
#DEFINE_CONSTANT MSG_START						"\x07"                  
#DEFINE_CONSTANT MSG_READ						"\x01"                  
#DEFINE_CONSTANT MSG_WRITE						"\x02"
#DEFINE_CONSTANT MSG_RETURN						"\x00"                  
#DEFINE_CONSTANT MSG_END						"\x08"

// COMMAND DATA
#DEFINE_CONSTANT CMD_PWR						"POW"
#DEFINE_CONSTANT CMD_INPUT						"MIN"
#DEFINE_CONSTANT CMD_VOL						"VOL"
#DEFINE_CONSTANT CMD_BACKLIGHT					"BRI"
#DEFINE_CONSTANT CMD_MUTE						"MUT"                                     
#DEFINE_CONSTANT CMD_WAKE_MODE					"WFS"                                             
#DEFINE_CONSTANT CMD_REMOTE						"RCU"
#DEFINE_CONSTANT CMD_VERSION					"GVE"
#DEFINE_CONSTANT CMD_SERIAL_NUM					"SER"
#DEFINE_CONSTANT CMD_MODEL_NAME					"MNA"

#DEFINE_CONSTANT CMD_STATE_OFF					"\x00"
#DEFINE_CONSTANT CMD_STATE_ON					"\x01"

// MESSAGE LOCATIONS
#DEFINE_CONSTANT MSG_LOC_START					1
#DEFINE_CONSTANT MSG_LOC_IDT					2
#DEFINE_CONSTANT MSG_LOC_TYPE					3
#DEFINE_CONSTANT MSG_LOC_CMD_BEGIN				4
#DEFINE_CONSTANT MSG_LOC_CMD_END				6
#DEFINE_CONSTANT MSG_LOC_STATUS					7

#HELP_BEGIN
To be used with Planar PS-Series (PS4661T_PS5561T) RS232 v1.0.umc or Planar PS-Series (PS4661T_PS5561T) IP v1.0.umc modules only.
Not to be used as a stand-alone module. 
#HELP_END

/*******************************************************************************************
  Include Libraries
  (Uncomment and include additional libraries as needed)
*******************************************************************************************/
// #CRESTRON_LIBRARY ""
// #USER_LIBRARY ""

/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/

DIGITAL_INPUT 	_SKIP_,Connect, Disconnect, Initialize,	_SKIP_, Power_On, Power_Off, Power_Toggle, 
				_SKIP_, Volume_Up, Volume_Down,	Volume_Mute_On, Volume_Mute_Off, Volume_Mute_Toggle, 
				_SKIP_, Backlight_Up, Backlight_Down, _SKIP_, Poll_Enable, _SKIP_, Input_Cycle,   
				Wake_Mode_Cycle, _SKIP_, Inputs[MAX_INPUTS], _SKIP_, Wake_Modes[MAX_WAKE_MODES], _SKIP_, 
				Remote_Commands[MAX_REMOTE_COMMANDS], _SKIP_;
    
ANALOG_INPUT 	Volume_Set, _SKIP_, Backlight_Set, _SKIP_;

BUFFER_INPUT 	From_Device[MAX_BUFFER_SIZE];  
         
/*----------------------------------------------------------------------------------------*/

DIGITAL_OUTPUT 	_SKIP_,Is_Communicating, _SKIP_, Is_Initialized,_SKIP_, Power_Is_On, _SKIP_, _SKIP_, 
				_SKIP_, _SKIP_, _SKIP_, Volume_Is_Muted, _SKIP_, _SKIP_, _SKIP_, _SKIP_, _SKIP_, _SKIP_, 
				Is_Polling, _SKIP_, _SKIP_, _SKIP_, _SKIP_,	Inputs_FB[MAX_INPUTS], _SKIP_,	
				Wake_Modes_FB[MAX_WAKE_MODES], _SKIP_, _SKIP_, _SKIP_, _SKIP_, _SKIP_, _SKIP_, _SKIP_, 
				_SKIP_,	_SKIP_, _SKIP_;
					       
ANALOG_OUTPUT 	Volume_Level, _SKIP_, Backlight_Level, _SKIP_;

STRING_OUTPUT 	To_Device;

/*******************************************************************************************
  SOCKETS
  (Uncomment and define socket definitions as needed)
*******************************************************************************************/
// TCP_CLIENT
// TCP_SERVER
// UDP_SOCKET

/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/

INTEGER_PARAMETER Monitor_ID, Volume_Step_Size, Backlight_Step_Size;

/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/
/*
#BEGIN_PARAMETER_PROPERTIES parameter_variable, parameter_variable, ...
   // propValidUnits = // unitString or unitDecimal|unitHex|unitPercent|unitCharacter|unitTime|unitTicks;
   // propDefaultUnit = // unitString, unitDecimal, unitHex, unitPercent, unitCharacter, unitTime or unitTicks;
   // propBounds = lower_bound , upper_bound;
   // propDefaultValue = ;  // or, propDefaultValue = "";
   // propList = // { "value" , "label" } , { "value" , "label" } , ... ;
   // propShortDescription = "status_bar_hint_text";
   // #BEGIN_PROP_FULL_DESCRIPTION  line_1...  line_2...  line_n  #END_PROP_FULL_DESCRIPTION
   // #BEGIN_PROP_NOTES line_1...  line_2...  line_n  #END_PROP_NOTES
#END_PARAMETER_PROPERTIES
*/

/*******************************************************************************************
  Structure Definitions
  (Uncomment and define structure definitions as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: struct.myString = "";
*******************************************************************************************/

STRUCTURE _ModuleStatus
{
	INTEGER nIsCommunicating;
	INTEGER nIsInitializing;
	INTEGER nIsInitialized;	
	INTEGER nIsParsing;
	INTEGER nIsHeartbeating;
	INTEGER nLastOutWasHeartbeat;
	INTEGER nIsPolling;
	STRING 	sLastMsgIn[MAX_RESPONSE_SIZE];
};

STRUCTURE _DeviceStatus
{
	INTEGER nPower;
	INTEGER nInput;
	INTEGER nVolumeLevel;
	INTEGER nVolumeMute;
	INTEGER nBacklightLevel;
	INTEGER nWakeMode; 
};

STRUCTURE _Queue
{
	INTEGER nBusy;
	INTEGER nHasItems;
	INTEGER nCommandHead;
	INTEGER nCommandTail;
	INTEGER nStatusHead;
	INTEGER nStatusTail;
	INTEGER nStrikeCount;
	STRING  sLastMsgOut[MAX_COMMAND_SIZE];
};

_ModuleStatus uModule;
_DeviceStatus uDevice;
_Queue uQueue;

/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/

STRING sCommandQueue[MAX_QUEUE_ITEMS][MAX_COMMAND_SIZE];
STRING sStatusQueue[MAX_QUEUE_ITEMS][MAX_COMMAND_SIZE];

STRING sInputCommands[MAX_INPUTS][1];
STRING sWakeModeCommands[MAX_WAKE_MODES][1];
STRING sRemoteCommands[MAX_REMOTE_COMMANDS][1];

/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/
     
////////////////////////////////////////////////////////////
// String Method Helpers
////////////////////////////////////////////////////////////

INTEGER_FUNCTION contains(STRING matchString, STRING sourceString)
{
	RETURN (FIND(matchString,sourceString) > 0); 
}

STRING_FUNCTION getBoundString(STRING source, STRING startString, STRING endString)
{
	INTEGER startIndex;
	INTEGER endIndex;
	STRING response[MAX_COMMAND_SIZE];
                                     
    response = "";

	IF (LEN(source) > 0)
	{
		startIndex = FIND(startString, source);
		endIndex = FIND(endString, source, startIndex + 1);
	
		IF (startIndex > 0 && startIndex < endIndex)
		{
			startIndex = startIndex + LEN(startString);
			response = MID(source, startIndex, endIndex - startIndex);
		}
	}

	RETURN (response);
}   

////////////////////////////////////////////////////////////
// Protocol Method Helpers	
////////////////////////////////////////////////////////////

STRING_FUNCTION buildSetMsg(INTEGER monitorID, STRING command, STRING value)
{
	STRING msg[MAX_COMMAND_SIZE];
        
    MAKESTRING(msg, "%s%s", msg, MSG_START);
    MAKESTRING(msg, "%s%s", msg, CHR(monitorID));
    MAKESTRING(msg, "%s%s", msg, MSG_WRITE);
    MAKESTRING(msg, "%s%s", msg, command);
    MAKESTRING(msg, "%s%s", msg, value);
    MAKESTRING(msg, "%s%s", msg, MSG_END);
    
    RETURN (msg);
}

STRING_FUNCTION buildGetMsg(INTEGER monitorID, STRING command)
{
	STRING msg[MAX_COMMAND_SIZE];
    
    MAKESTRING(msg, "%s%s", msg, MSG_START);
    MAKESTRING(msg, "%s%s", msg, CHR(monitorID));
    MAKESTRING(msg, "%s%s", msg, MSG_READ);
    MAKESTRING(msg, "%s%s", msg, command);
    MAKESTRING(msg, "%s%s", msg, MSG_END);
    
    RETURN (msg);
}

////////////////////////////////////////////////////////////
// Reset Helpers
////////////////////////////////////////////////////////////

FUNCTION resetQueue()
{
	uQueue.nBusy = FALSE;
	uQueue.nHasItems = FALSE;
	uQueue.nCommandHead = 1;
	uQueue.nCommandTail = 1;
	uQueue.nStatusHead = 1;
	uQueue.nStatusTail = 1;
	uQueue.nStrikeCount = 0;	
	uQueue.sLastMsgOut = BLANK_STRING;         
}

FUNCTION resetModule()
{
	uModule.nIsCommunicating = FALSE;
	uModule.nIsInitializing = FALSE;
	uModule.nIsInitialized = FALSE;
	uModule.nIsParsing = FALSE;
	uModule.nIsPolling = FALSE;
	uModule.sLastMsgIn = BLANK_STRING;
	
	Is_Communicating = FALSE;
	Is_Initialized = FALSE;
}

FUNCTION resetDevice()
{
	INTEGER i;
	
	uDevice.nPower = NULL;
	uDevice.nInput = NULL;
	uDevice.nVolumeLevel = NULL;
	uDevice.nVolumeMute = NULL;
	uDevice.nBacklightLevel = NULL;
	uDevice.nWakeMode = NULL;
	
	Power_Is_On = FALSE;
	Volume_Is_Muted = FALSE;
	Volume_Level = 0;
	Backlight_Level = 0;
	
	FOR(i = 1 TO MAX_INPUTS)
		Inputs_FB[i] = FALSE; 
	
	FOR(i = 1 TO MAX_WAKE_MODES)
		Wake_Modes_FB[i] = FALSE;
}

FUNCTION reset()
{              
	trace("reset");

	resetQueue();
    resetModule();
    resetDevice();
}

////////////////////////////////////////////////////////////
// Queue Helpers
////////////////////////////////////////////////////////////

INTEGER_FUNCTION isQueueEmpty()
{
	RETURN (uQueue.nHasItems = FALSE && uQueue.nBusy = FALSE);
}

STRING_FUNCTION deQueue()
{
	STRING sCmd[MAX_COMMAND_SIZE];
                        
	sCmd = BLANK_STRING;

	IF (uQueue.nHasItems = TRUE && uQueue.nBusy = FALSE)
	{        
		uQueue.nBusy = TRUE;

		// Priority Queue has items
		IF (uQueue.nCommandHead != uQueue.nCommandTail)
		{
			IF (uQueue.nCommandTail = GETNUMARRAYROWS(sCommandQueue))
				uQueue.nCommandTail = 1;
			ELSE
				uQueue.nCommandTail = uQueue.nCommandTail + 1;

			uQueue.sLastMsgOut = sCommandQueue[uQueue.nCommandTail];
		}
		// Secondary Queue has items
		ELSE IF (uQueue.nStatusHead != uQueue.nStatusTail)
		{
			IF (uQueue.nStatusTail = GETNUMARRAYROWS(sStatusQueue))
				uQueue.nStatusTail = 1;
			ELSE
				uQueue.nStatusTail = uQueue.nStatusTail + 1;

			uQueue.sLastMsgOut = sStatusQueue[uQueue.nStatusTail];
		}
	
		IF (uQueue.nCommandHead = uQueue.nCommandTail && uQueue.nStatusHead = uQueue.nStatusTail)
			uQueue.nHasItems = FALSE;         
        
		sCmd = uQueue.sLastMsgOut;
	}

	RETURN (sCmd);
}

FUNCTION sendNextQueueItem()
{
	STRING sOutgoing[MAX_COMMAND_SIZE];
    STRING sGarbage[MAX_COMMAND_SIZE];        
	
	sOutgoing = deQueue();
	
	IF (LEN(sOutgoing) > 0)
	{
		TRACE("sendNextQueueItem() - sending next message");
        
        IF(contains("<HEARTBEAT>", sOutgoing) = TRUE)
        {
     		sGarbage = REMOVE("<HEARTBEAT>", sOutgoing);
     		uModule.nLastOutWasHeartbeat = TRUE;  	
        }   
        ELSE
        	uModule.nLastOutWasHeartbeat = FALSE; 
        
		MAKESTRING(To_Device, "%s", sOutgoing);
         
		WAIT (250, QueueFalseResponse)
		{
			IF (uQueue.nBusy = TRUE)
			{
				uQueue.nBusy = FALSE;
				
				IF (uQueue.nStrikeCount < 3)
				{
					uQueue.nStrikeCount = uQueue.nStrikeCount + 1;

					TRACE("Failed Response"); 
					TRACE("Strike Count[%u]",uQueue.nStrikeCount);
					
					//uModule.nIsInitializing = FALSE;
					
					sendNextQueueItem();
				}
				ELSE
					reset();
			}
		}
	}
	ELSE
		TRACE("sendNextQueueItem() - nothing to send");
}

FUNCTION enQueue(STRING sCmd, INTEGER bPriority)
{
	INTEGER nQueueWasEmpty;

	nQueueWasEmpty = isQueueEmpty();
    
	IF (bPriority = TRUE)
	{
		IF (uQueue.nCommandHead = GETNUMARRAYROWS(sCommandQueue))
		{
			IF (uQueue.nCommandTail != 1)
			{
				uQueue.nCommandHead = 1;
				sCommandQueue[uQueue.nCommandHead] = sCmd;
				uQueue.nHasItems = TRUE;
			}
		}
		ELSE IF (uQueue.nCommandTail != (uQueue.nCommandHead + 1))
		{
			uQueue.nCommandHead = uQueue.nCommandHead + 1;
			sCommandQueue[uQueue.nCommandHead] = sCmd;
			uQueue.nHasItems = TRUE;
		}
	}
	ELSE
	{
		IF (uQueue.nStatusHead = GETNUMARRAYROWS(sStatusQueue))
		{
			IF (uQueue.nStatusTail != 1)
			{
				uQueue.nStatusHead = 1;
				sStatusQueue[uQueue.nStatusHead] = sCmd;
				uQueue.nHasItems = TRUE;
			}
		}
		ELSE IF (uQueue.nStatusTail != (uQueue.nStatusHead + 1))
		{
			uQueue.nStatusHead = uQueue.nStatusHead + 1;
			sStatusQueue[uQueue.nStatusHead] = sCmd;
			uQueue.nHasItems = TRUE;
		}
	}

	IF (nQueueWasEmpty)
		sendNextQueueItem();
}

////////////////////////////////////////////////////////////
// Device Command Helpers
////////////////////////////////////////////////////////////

FUNCTION setPower(INTEGER monitorID, INTEGER state)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	IF(state = TRUE || state = FALSE)
	{
       	SWITCH(state)
       	{
      		CASE (TRUE):  MAKESTRING(sCommand,"%s", buildSetMsg(monitorID, CMD_PWR, CMD_STATE_ON));
      		CASE (FALSE): MAKESTRING(sCommand,"%s", buildSetMsg(monitorID, CMD_PWR, CMD_STATE_OFF));		
       	} 
       	
       	enQueue(sCommand, TRUE);
    }	
}

FUNCTION getPower(INTEGER monitorID)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
   	MAKESTRING(sCommand,"%s", buildGetMsg(monitorID, CMD_PWR));
    enQueue(sCommand, FALSE);		
}

FUNCTION setInput(INTEGER monitorID, INTEGER input)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	IF(Is_Initialized = TRUE && uDevice.nPower = TRUE)
	{
		IF(input > 0 && input <= MAX_INPUTS)
		{
			MAKESTRING(sCommand,"%s", buildSetMsg(monitorID, CMD_INPUT, sInputCommands[input]));
    		enQueue(sCommand, TRUE);		
    	}
	}		
}

FUNCTION getInput(INTEGER monitorID)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	MAKESTRING(sCommand,"%s", buildGetMsg(monitorID, CMD_INPUT));
    enQueue(sCommand, FALSE);		
}

FUNCTION setVolume(INTEGER monitorID, INTEGER level)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	IF(Is_Initialized = TRUE && uDevice.nPower = TRUE)
	{
		IF(level >= MIN_VOL_LVL && level <= MAX_VOL_LVL)
		{
			MAKESTRING(sCommand,"%s", buildSetMsg(monitorID, CMD_VOL, CHR(level)));
    		enQueue(sCommand, TRUE);
		}
	}
}

FUNCTION getVolume(INTEGER monitorID)
{
    STRING sCommand[MAX_COMMAND_SIZE];
	
	MAKESTRING(sCommand,"%s", buildGetMsg(monitorID, CMD_VOL));
    enQueue(sCommand, FALSE);		
}

FUNCTION setVolumeMute(INTEGER monitorID, INTEGER state)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	IF(Is_Initialized = TRUE && uDevice.nPower = TRUE)
	{
		IF(state = TRUE || state = FALSE)
		{
         	SWITCH(state)
         	{
        		CASE (TRUE):  MakeString(sCommand,"%s", buildSetMsg(monitorID, CMD_MUTE, CMD_STATE_ON));
        		CASE (FALSE): MakeString(sCommand,"%s", buildSetMsg(monitorID, CMD_MUTE, CMD_STATE_OFF));		
         	} 
         	
         	enQueue(sCommand, TRUE);
		}	
	}		
}

FUNCTION getVolumeMute(INTEGER monitorID)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	MAKESTRING(sCommand,"%s", buildGetMsg(monitorID, CMD_MUTE));
    enQueue(sCommand, FALSE);		
}

FUNCTION setWakeMode(INTEGER monitorID, INTEGER mode)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	IF(Is_Initialized = TRUE && uDevice.nPower = TRUE)
	{
		IF(mode > 0 && mode <= MAX_WAKE_MODES)
		{
		    MAKESTRING(sCommand,"%s", buildSetMsg(monitorID, CMD_WAKE_MODE, sWakeModeCommands[mode]));
    		enQueue(sCommand, TRUE);
		}
	}		
}

FUNCTION getWakeMode(INTEGER monitorID)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	MAKESTRING(sCommand,"%s", buildGetMsg(monitorID, CMD_WAKE_MODE));
    enQueue(sCommand, FALSE);		
}

FUNCTION sendIR(INTEGER monitorID, INTEGER irFunction)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	IF(Is_Initialized = TRUE && uDevice.nPower = TRUE)
	{
		IF(irFunction > 0 && irFunction <= MAX_REMOTE_COMMANDS)
		{
		    MAKESTRING(sCommand,"%s", buildSetMsg(monitorID, CMD_REMOTE, sRemoteCommands[irFunction]));
    		enQueue(sCommand, TRUE);
		}
	}		
}

FUNCTION setBacklight(INTEGER monitorID, INTEGER level)
{
	STRING sCommand[MAX_COMMAND_SIZE];
	
	IF(Is_Initialized = TRUE && uDevice.nPower = TRUE)
	{
		IF(level >= MIN_BACKLIGHT_LVL && level <= MAX_BACKLIGHT_LVL)
		{
			MAKESTRING(sCommand,"%s", buildSetMsg(monitorID, CMD_BACKLIGHT, CHR(level)));
    		enQueue(sCommand, TRUE);
		}
	}
}

FUNCTION getBacklight(INTEGER monitorID)
{
    STRING sCommand[MAX_COMMAND_SIZE];
	
	MAKESTRING(sCommand,"%s", buildGetMsg(monitorID, CMD_BACKLIGHT));
    enQueue(sCommand, FALSE);		
}

////////////////////////////////////////////////////////////
// Polling Helpers
////////////////////////////////////////////////////////////

FUNCTION getCurrentStatusAll()
{
	getPower(Monitor_ID);
	getInput(Monitor_ID);
	getVolume(Monitor_ID);
	getVolumeMute(Monitor_ID);
	getBacklight(Monitor_ID);
    getWakeMode(Monitor_ID);
}

FUNCTION sendPoll()
{
	IF (uModule.nIsPolling = TRUE && uDevice.nPower = TRUE)
	{
		TRACE("sendPoll()");
		
		getCurrentStatusAll();

		WAIT (POLL_TIME, POLL)
			IF (uModule.nIsPolling = TRUE)
				sendPoll();
	}
}

FUNCTION startPoll()
{
	IF (uModule.nIsPolling = FALSE)
	{   
		uModule.nIsPolling = TRUE; 
		Is_Polling = TRUE;
		sendPoll();		
	}
}

FUNCTION stopPoll()
{
	uModule.nIsPolling = FALSE;
	Is_Polling = FALSE;
	CANCELWAIT(POLL);
}

////////////////////////////////////////////////////////////
// Initialize Helpers
////////////////////////////////////////////////////////////

INTEGER_FUNCTION isInitialized()
{
	IF(	uDevice.nPower = NULL || uDevice.nInput = NULL || uDevice.nVolumeLevel = NULL || 
		uDevice.nVolumeMute = NULL || uDevice.nWakeMode = NULL || uDevice.nBacklightLevel = NULL)
			RETURN (FALSE);
	
	RETURN (TRUE);
}

FUNCTION getInitialized()
{
	TRACE("getInitialized()");

	IF(uModule.nIsInitializing = FALSE && isInitialized() = FALSE)
	{
	    TRACE("passed test");
	    
	    uModule.nIsInitializing = TRUE;
		getCurrentStatusAll();
	}
}

////////////////////////////////////////////////////////////
// Response Helpers
////////////////////////////////////////////////////////////

FUNCTION goodResponse()
{
	TRACE("goodResponse()");

   	uModule.nIsCommunicating = TRUE; 
   	Is_Communicating = TRUE; 
    
	uQueue.nStrikeCount = 0; 
      
    IF(uModule.nLastOutWasHeartbeat = TRUE && uDevice.nPower = TRUE && uModule.nIsInitializing = FALSE)
    	getInitialized();
    
	uQueue.nBusy = FALSE;

	sendNextQueueItem();
}

FUNCTION ProcessDeviceMsg()
{
	STRING sStatus[MAX_COMMAND_SIZE];
	INTEGER nStatus;
	INTEGER i;
	
	IF (LEN(uModule.sLastMsgIn) > 0)
	{		
		IF(contains(MSG_END, uModule.sLastMsgIn) = TRUE)
		{
	        IF(contains(CMD_PWR, uModule.sLastMsgIn) = TRUE)
			{
				sStatus = getBoundString(uModule.sLastMsgIn, CMD_PWR, MSG_END);
				
				IF(sStatus = CMD_STATE_ON)
            	{
            		uDevice.nPower = TRUE;
            		Power_Is_On = TRUE;
            	}
	            ELSE IF(sStatus = CMD_STATE_OFF)
				{
					uDevice.nPower = FALSE;
					Power_Is_On = FALSE;			
				}
			}
			ELSE IF(contains(CMD_VOL, uModule.sLastMsgIn) = TRUE)
			{
				nStatus = BYTE(uModule.sLastMsgIn, MSG_LOC_STATUS); 
				
               	uDevice.nVolumeLevel = nStatus;
               	Volume_Level = nStatus;
			}
			ELSE IF(contains(CMD_MUTE, uModule.sLastMsgIn) = TRUE)
			{
				sStatus = getBoundString(uModule.sLastMsgIn, CMD_MUTE, MSG_END);
				
				IF(sStatus = CMD_STATE_ON)
            	{
            		uDevice.nVolumeMute = TRUE;
            		Volume_Is_Muted = TRUE;
            	}
	            ELSE IF(sStatus = CMD_STATE_OFF)
				{
					uDevice.nVolumeMute = FALSE;
					Volume_Is_Muted = FALSE;
				}
			}
			ELSE IF(contains(CMD_BACKLIGHT, uModule.sLastMsgIn) = TRUE)
			{
				nStatus = BYTE(uModule.sLastMsgIn, MSG_LOC_STATUS); 
				
               	uDevice.nBacklightLevel = nStatus;
               	Backlight_Level = nStatus;
			}
			ELSE IF(contains(CMD_INPUT, uModule.sLastMsgIn) = TRUE)
			{
				sStatus = getBoundString(uModule.sLastMsgIn, CMD_INPUT, MSG_END);
				
				FOR(i = 1 TO MAX_INPUTS)
                {
                	IF(sStatus = sInputCommands[i])
                	{
               			uDevice.nInput = i;
               			Inputs_FB[i] = TRUE; 		
                	}
                	ELSE
                 		Inputs_FB[i] = FALSE;
                }
			}
			ELSE IF(contains(CMD_WAKE_MODE, uModule.sLastMsgIn) = TRUE)
			{
				sStatus = getBoundString(uModule.sLastMsgIn, CMD_WAKE_MODE, MSG_END);
				
				FOR(i = 1 TO MAX_WAKE_MODES)
                {
                	IF(sStatus = sWakeModeCommands[i])
                	{
               			uDevice.nWakeMode = i;
               			Wake_Modes_FB[i] = TRUE; 		
                	}
                	ELSE
                		Wake_Modes_FB[i] = FALSE;
                }
			} 
			
			CANCELWAIT(QueueFalseResponse);
			goodResponse();
			
			IF(uModule.nIsInitializing = TRUE && isInitialized() = TRUE)
			{
				trace("initialization complete");
				
				uModule.nIsInitializing = FALSE;
				uModule.nIsInitialized = TRUE;
				Is_Initialized = TRUE;
				
				IF(Poll_Enable = TRUE && uModule.nIsPolling = FALSE)
					WAIT(POLL_TIME, StartPollingProcess)
						startPoll();
			}			
		}
	}
}

////////////////////////////////////////////////////////////
// Heartbeat Helpers
//////////////////////////////////////////////////////////// 

FUNCTION sendHeartbeat()
{
	STRING sCommand[MAX_COMMAND_SIZE];

	IF (uModule.nIsHeartbeating = TRUE)
	{
		IF (isQueueEmpty())
		{
			TRACE("sendHeartbeat()");
			
			MAKESTRING(sCommand,"<HEARTBEAT>%s", buildGetMsg(Monitor_ID, CMD_PWR));                               
			enQueue(sCommand, FALSE);

		}

		WAIT (HEARTBEAT_TIME, HEARTBEAT)
			IF (uModule.nIsHeartbeating = TRUE)
				sendHeartbeat();
	}		
}    

FUNCTION startHeartbeat()
{
	IF (uModule.nIsHeartbeating = FALSE)
	{   
		uModule.nIsHeartbeating = TRUE;
		sendHeartbeat();		
	}
}

FUNCTION stopHeartbeat()
{
	uModule.nIsHeartbeating = FALSE;
	CANCELWAIT(HEARTBEAT);
}

////////////////////////////////////////////////////////////
// Connect/Disconnect/Reinit Helpers
////////////////////////////////////////////////////////////

FUNCTION doConnect()
{
	TRACE("doConnect()");
		
	reset();
	CANCELWAIT(QueueFalseResponse);
	
	WAIT(500, StartHeartBeatProcess)
		startHeartbeat();
}   

FUNCTION doDisconnect()
{
	TRACE("doDisconnect()");
	
	reset();
	CANCELWAIT(StartHeartBeatProcess);
	CANCELWAIT(StartPollingProcess);
	CANCELWAIT(QueueFalseResponse);
	stopHeartbeat();
	stopPoll();
}

FUNCTION doReinitialize()
{
	TRACE("doReinitialize()");

	doDisconnect();
	doConnect();	
}

/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/

PUSH Connect
{
    doConnect();	
}

PUSH Disconnect
{
	doDisconnect();
}

PUSH Initialize
{
	doReinitialize();
}

PUSH Power_On
{
    setPower(Monitor_ID, TRUE);
}

PUSH Power_Off
{
    setPower(Monitor_ID, FALSE);
}

PUSH Power_Toggle
{
    IF(uDevice.nPower = TRUE)
    	setPower(Monitor_ID, FALSE); 
   	ELSE IF(uDevice.nPower = FALSE)
    	setPower(Monitor_ID, TRUE);
}

PUSH Volume_Up
{
    IF(uDevice.nVolumeLevel < MAX_VOL_LVL)
    {
  		IF((MAX_VOL_LVL - uDevice.nVolumeLevel) > Volume_Step_Size)
    		setVolume(Monitor_ID, uDevice.nVolumeLevel + Volume_Step_Size);
    	ELSE
    		setVolume(Monitor_ID, MAX_VOL_LVL);  	
    }
}

PUSH Volume_Down
{
    IF(uDevice.nVolumeLevel > MIN_VOL_LVL)
    {
  		IF((uDevice.nVolumeLevel - MIN_VOL_LVL) > Volume_Step_Size)
    		setVolume(Monitor_ID, uDevice.nVolumeLevel - Volume_Step_Size);
    	ELSE
    		setVolume(Monitor_ID, MIN_VOL_LVL);  	
    }
}

CHANGE Volume_Set
{
    IF(Volume_Set >= MIN_VOL_LVL && Volume_Set <= MAX_VOL_LVL)
    	setVolume(Monitor_ID, Volume_Set);
}

PUSH Volume_Mute_On
{
    setVolumeMute(Monitor_ID, TRUE);
}

PUSH Volume_Mute_Off
{
    setVolumeMute(Monitor_ID, FALSE);
}
 
PUSH Volume_Mute_Toggle
{
    IF(uDevice.nVolumeMute = TRUE)
    	setVolumeMute(Monitor_ID, FALSE); 
   	ELSE IF(uDevice.nVolumeMute = FALSE)
    	setVolumeMute(Monitor_ID, TRUE);
}

PUSH Backlight_Up
{
    IF(uDevice.nBacklightLevel < MAX_BACKLIGHT_LVL)
    {
  		IF((MAX_BACKLIGHT_LVL - uDevice.nBacklightLevel) > Backlight_Step_Size)
    		setBacklight(Monitor_ID, uDevice.nBacklightLevel + Backlight_Step_Size);
    	ELSE
    		setBacklight(Monitor_ID, MAX_BACKLIGHT_LVL);  	
    }
}

PUSH Backlight_Down
{
    IF(uDevice.nBacklightLevel > MIN_BACKLIGHT_LVL)
    {
  		IF((uDevice.nBacklightLevel - MIN_BACKLIGHT_LVL) > Backlight_Step_Size)
    		setBacklight(Monitor_ID, uDevice.nBacklightLevel - Backlight_Step_Size);
    	ELSE
    		setBacklight(Monitor_ID, MIN_BACKLIGHT_LVL);  	
    }
}

CHANGE Backlight_Set
{
    IF(Backlight_Set >= MIN_BACKLIGHT_LVL && Backlight_Set <= MAX_BACKLIGHT_LVL)
    	setBacklight(Monitor_ID, Backlight_Set);
}

PUSH Inputs
{
    INTEGER input;
    input = GETLASTMODIFIEDARRAYINDEX();
    
    setInput(Monitor_ID, input);
}

PUSH Input_Cycle
{
    IF(uDevice.nInput != NULL)
    {
    	IF(uDevice.nInput = MAX_INPUTS)
    		setInput(Monitor_ID, 1);
    	ELSE
    		setInput(Monitor_ID, uDevice.nInput + 1);
    }
}


PUSH Wake_Modes
{
    INTEGER mode;
    mode = GETLASTMODIFIEDARRAYINDEX();
    
    setWakeMode(Monitor_ID, mode);
}

PUSH Wake_Mode_Cycle
{
    IF(uDevice.nWakeMode != NULL)
    {
    	IF(uDevice.nWakeMode = MAX_WAKE_MODES)
    		setWakeMode(Monitor_ID, 1);
    	ELSE
    		setWakeMode(Monitor_ID, uDevice.nWakeMode + 1);
    }
}

PUSH Remote_Commands
{
	INTEGER irFunction;
    irFunction = GETLASTMODIFIEDARRAYINDEX();
    
    sendIR(Monitor_ID, irFunction);
}

PUSH Poll_Enable
{
    IF(uModule.nIsPolling = FALSE)
    	startPoll();
}

RELEASE Poll_Enable
{
    IF(uModule.nIsPolling = TRUE)
    	stopPoll();
}

#IF_SERIES3
THREADSAFE CHANGE From_Device
{           
	STRING 	msgToCmdEnd[MAX_COMMAND_SIZE];
	STRING 	msgRemainder[MAX_COMMAND_SIZE];
	
	WHILE(TRUE)
	{
		TRY                       
		{
			msgToCmdEnd = GATHERBYLENGTH(MSG_LOC_CMD_END, From_Device);
			
			// CMD_VERSION gives 6 byte response + 1 for delimiter
			IF(contains(CMD_VERSION, msgToCmdEnd) = TRUE)
				msgRemainder = GATHERBYLENGTH(7, From_Device);			
			
			// CMD_SERIAL_NUM & CMD_MODEL_NAME give 13 byte response + 1 for delimiter
			ELSE IF(contains(CMD_SERIAL_NUM, msgToCmdEnd) = TRUE || contains(CMD_MODEL_NAME, msgToCmdEnd) = TRUE)
				msgRemainder = GATHERBYLENGTH(14, From_Device);
				
			// all other command gives 1 byte response + 1 for delimiter				
            ELSE
				msgRemainder = GATHERBYLENGTH(2, From_Device);			
			
			MAKESTRING(uModule.sLastMsgIn,"%s%s", msgToCmdEnd, msgRemainder);
			ProcessDeviceMsg();													
		}
		CATCH
		{
			TRACE("Issue with Device message handling\n");
		}		
	}
}
#ELSE
CHANGE From_Device
{
    STRING 	msgToCmdEnd[MAX_COMMAND_SIZE];
	STRING 	msgRemainder[MAX_COMMAND_SIZE];
	
    IF (uModule.nIsParsing = FALSE)
    {
    	uModule.nIsParsing = TRUE;
    	
    	WHILE(TRUE)
    	{
			msgToCmdEnd = GATHERBYLENGTH(MSG_LOC_CMD_END, From_Device);
			
			// CMD_VERSION gives 6 byte response + 1 for delimiter
			IF(contains(CMD_VERSION, msgToCmdEnd) = TRUE)
				msgRemainder = GATHERBYLENGTH(7, From_Device);			
			
			// CMD_SERIAL_NUM & CMD_MODEL_NAME give 13 byte response + 1 for delimiter
			ELSE IF(contains(CMD_SERIAL_NUM, msgToCmdEnd) = TRUE || contains(CMD_MODEL_NAME, msgToCmdEnd) = TRUE)
				msgRemainder = GATHERBYLENGTH(14, From_Device);
				
			// all other command gives 1 byte response + 1 for delimiter				
            ELSE
				msgRemainder = GATHERBYLENGTH(2, From_Device);			
			
			MAKESTRING(uModule.sLastMsgIn,"%s%s", msgToCmdEnd, msgRemainder);
			ProcessDeviceMsg();
		}
    	
    	uModule.nIsParsing = FALSE;
    }
}
#ENDIF

/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/

FUNCTION Main()
{
	sInputCommands[1] 		= "\x00";	// VGA
	sInputCommands[2] 		= "\x01";	// DVI
	sInputCommands[3] 		= "\x09";	// HDMI 1
	sInputCommands[4] 		= "\x0D";	// DisplayPort
    
    sWakeModeCommands[1]	= "\x00";	// Wake VGA
    sWakeModeCommands[2] 	= "\x01";	// Wake VGA/Digital/RS232
    sWakeModeCommands[3] 	= "\x02";	// Never Sleep
        
    sRemoteCommands[1] 		= "\x00";	// Menu
    sRemoteCommands[2] 		= "\x01";	// Info
    sRemoteCommands[3] 		= "\x02";	// Up
    sRemoteCommands[4] 		= "\x03";	// Down
    sRemoteCommands[5] 		= "\x04";	// Left
    sRemoteCommands[6] 		= "\x05";	// Right
    sRemoteCommands[7] 		= "\x06";	// Enter
    sRemoteCommands[8] 		= "\x07";	// Exit
 
	uModule.nIsHeartbeating = FALSE;
	uModule.nIsPolling = FALSE;
	
	reset();
	
	WAITFORINITIALIZATIONCOMPLETE();
}

